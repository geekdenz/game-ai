apply plugin: 'java'
apply plugin: 'application'

ext.moduleName = 'nz.dcoder.ai'

mainClassName = "$moduleName/nz.dcoder.ai.astar.BoardSearch"
version = '0.1.0'
sourceCompatibility = '1.9'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava {
	inputs.property("moduleName", moduleName)
	doFirst {
		options.compilerArgs += [
			'--module-path', classpath.asPath,
		]
		classpath = files()
	}

	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

compileTestJava {
	inputs.property("moduleName", moduleName)
	doFirst {
		options.compilerArgs += [
			'--module-path', classpath.asPath,
			'--add-modules', 'junit',
			'--add-reads', "$moduleName=junit",
			'--path-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
		]
	}

	options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

test {
	inputs.property("moduleName", moduleName)
	doFirst {
		jvmArgs += [
			'--modulePath', classpath.asPath,
			'--add-modules', 'ALL-MODULE-PATH',
			'--add-reads', "$moduleName=junit",
			'--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
		]
	}
}

run {
	inputs.property("moduleName", moduleName)
	doFirst {
		jvmArgs = [
			'--module-path', classpath.asPath,
			'--module', mainClassName,
		]
		classpath = files()
	}
}

startScripts {
	inputs.property("moduleName", moduleName)
	doFirst {
		classpath = files()
		defaultJvmOpts = [
			'--module-path', 'APP_HOME_LIBS',
			'--module', mainClassName,
		]
	}

	doLast {
		def bashFile = new File(outputDir, applicationName)
		String bashContent = bashFile.text
		bashFile.text = bashContent.replaceFirst('APP_HOME_LIBS', java.util.regex.Matcher.quoteReplacement('$APP_HOME/lib'))

		def batFile = new File(outputDir, applicationName + ".bat")
		String batContent = batFile.text
		batFile.text = batContent.replaceFirst('APP_HOME_LIBS', java.util.regex.Matcher.quoteReplacement('%APP_HOME%\\lib'))
	}
}

